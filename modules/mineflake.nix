{ lib, pkgs, config, ... }:

with lib; let
  cfg = config.minecraft;

  mkConfigDerivation = name: server-name: option:
    let
      default = {
        version = server-name;
        phases = [ "buildPhase" "installPhase" ];
        pname = "minecraft-config-${name}";
        installPhase = "chmod 744 $out";
      };
    in
    (
      # yaml compatible with json format
      if option.type == "yaml" || option.type == "json" then
        pkgs.stdenv.mkDerivation
          (default // {
            buildPhase = ''
              cat <<EOF > $out
              ${builtins.toJSON option.data}
              EOF
            '';
          })
      else if option.type == "raw" then
        pkgs.stdenv.mkDerivation
          (default // {
            buildPhase = ''
              cat <<EOF > $out
              ${option.data.raw}
              EOF
            '';
          })
      else
        pkgs.stdenv.mkDerivation (default // {
          buildPhase = ''
            cat <<EOF > $out
            none
            EOF
          '';
        })
    );

  mkConfigs = server: server-name:
    let ders = mapAttrs (name: option: mkConfigDerivation name server-name option) server.configs; in
    (
      toString (map
        (key: ''
          # Link "${key}" config file
          rm -f ${server.datadir}/data/${key}
          ln -sf ${getAttr key ders} ${server.datadir}/data/${key}
        '')
        (attrNames ders))
    );

  configSubmodule = types.submodule
    ({ ... }: {
      options = {
        type = mkOption {
          type = types.enum [ "yaml" "json" "raw" ];
          default = "yaml";
          description = "Config type.";
        };

        data = mkOption {
          type = types.anything;
          description = "Config contents.";
        };
      };
    }) // {
    description = "Config submodule";
  };

  serverSubmodule = types.submodule
    ({ ... }: {
      options = {
        datadir = mkOption {
          type = types.path;
          # TODO: add default value based on ${name}
          description = "Server data directory.";
        };

        opts = mkOption {
          type = types.listOf types.str;
          default = [ "nogui" ];
          description = "Server launch options.";
        };

        configs = mkOption {
          type = types.attrsOf configSubmodule;
          description = "Config derivations.";
        };

        jre = mkOption {
          type = types.package;
          default = pkgs.jre;
          description = "Java package.";
        };

        plugins = mkOption {
          type = types.listOf types.package;
          description = "Plugins to install.";
        };

        package = mkOption {
          type = types.package;
          default = pkgs.callPackage ../pkgs/spigot/paper { };
          example = "pkgs.spigot.paper";
          description = "Server package.";
        };
      };
    }) // {
    description = "Server submodule";
  };
in
{
  options.minecraft = {
    enable = mkOption {
      type = types.bool;
      default = false;
      description = ''
        If enabled, Nix-defined minecraft servers will be created from minecraft.servers.
      '';
    };

    servers = mkOption {
      type = types.attrsOf serverSubmodule;
      description = ''
        List of server submodules to create.
      '';
    };
  };

  config = mkIf cfg.enable {
    systemd.services =
      let
        obj = builtins.mapAttrs
          (name: server:
            {
              name = "minecraft-${name}";
              value = {
                restartIfChanged = true;
                wantedBy = [ "multi-user.target" ];
                wants = [ "minecraft-${name}-prepare.service" "network-online.target" ];
                after = [ "minecraft-${name}-prepare.service" "network-online.target" ];
                description = "Launch ${name} mineflake server configuration.";
                serviceConfig = {
                  Type = "simple";
                  ProtectHome = true;
                  ProtectSystem = true;
                  User = "minecraft-${name}";
                  Group = "minecraft-${name}";
                  SyslogIdentifier = "minecraft-${name}";
                  WorkingDirectory = "${server.datadir}/data";
                };
                script = ''
                  # Generated by mineflake. Do not edit this file.

                  # Change directory to server data
                  cd ${server.datadir}/data

                  # eula.txt generation
                  echo "# Generated by mineflake. Do not edit this file." > "${server.datadir}/data/eula.txt"
                  echo "eula=true" >> "${server.datadir}/data/eula.txt"

                  # Remove old plugin symlinks
                  rm -rf "${server.datadir}/data/plugins/*.jar"

                  ${builtins.toString ( builtins.map (
                    plugin:
                      ''
                        # Link plugin ${plugin.pname} ${plugin.version}
                        ln -sf ${plugin}/result "${server.datadir}/data/plugins/${plugin.pname}-${plugin.version}-${plugin.hash}.jar"
                      '' + "\n"
                    ) server.plugins)}

                  ${mkConfigs server name}

                  # Launch ${server.package.pname} ${server.package.version}
                  ${server.jre}/bin/java -jar ${server.package}/result ${builtins.toString (builtins.map (x: "\""+x+"\"") server.opts)}
                '';
              };
            })
          cfg.servers;
          obj_prepare = builtins.mapAttrs
          (name: server:
            {
              name = "minecraft-${name}-prepare";
              value = {
                wantedBy = [ "multi-user.target" ];
                wants = [ "network-online.target" ];
                after = [ "network-online.target" ];
                description = "Prepare ${name} server folder.";
                serviceConfig = {
                  User = "root";
                  Type = "oneshot";
                  RemainAfterExit = "yes";
                  SyslogIdentifier = "minecraft-${name}-prepare";
                };
                script = ''
                  # Generated by mineflake. Do not edit this file.

                  # Create required directories
                  mkdir -p ${server.datadir}/data/plugins

                  # Give rights to folder
                  chown -R minecraft-${name}:minecraft-${name} ${server.datadir}
                  chmod -R 777 ${server.datadir}
                '';
              };
            })
          cfg.servers;
      in
      builtins.listToAttrs (map (key: getAttr key obj) (attrNames obj)) // builtins.listToAttrs (map (key: getAttr key obj_prepare) (attrNames obj_prepare));

    users.users =
      let
        obj = builtins.mapAttrs
          (name: server:
            {
              name = "minecraft-${name}";
              value = {
                createHome = true;
                isSystemUser = true;
                home = server.datadir;
                group = "minecraft-main";
                description = "System account that runs ${name} mineflake server configuration.";
              };
            })
          cfg.servers;
      in
      builtins.listToAttrs (map (key: getAttr key obj) (attrNames obj));

    users.groups =
      let
        obj = builtins.mapAttrs
          (name: server:
            {
              name = "minecraft-${name}";
              value = {
                name = "minecraft-${name}";
                members = [ "minecraft-${name}" ];
              };
            })
          cfg.servers;
      in
      builtins.listToAttrs (map (key: getAttr key obj) (attrNames obj));
  };
}
